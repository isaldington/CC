/*
    Type: Slash Command
    Trigger: guessword
    Min Perms: None
    Ignore Roles: None
    Run only in: #game-room
    channelUsed: Run in Channel
*/
$onlyIf[$authorID==$ownerID;{private}{color:db3539}{title:Coming soon!}]
$function[getImage;id;i]{
    $let[index;$conditional[$i!=undefined;$i;0]]
    $return[$msg[1157421810288185384;$id;attachment;$index]]
}
$let[data;$getUserVar[wordGuessSession]]
$if[$isValidObject[$data]==true]{
    $objectCreate[$data]
    $let[sessionURL;$msg[$objectGet[session;channel];$objectGet[session;message];url]]
}
$onlyIf[$data==undefined;
    {private}
    {color:db3539}
    {author:Error:$getImage[1157974096198508604;6]}
    {desc:You already have an active session. [Click here]($sessionURL) to jump to the session.}
]
$let[numGuess;10]
$objectCreate[[
    "- Click on `Start` to begin the game or `Quit` to quit.",
    "- A word will be randomly chosen. The objective of the game is to guess the word.",
    "- You are given **$numGuess** attempts to guess the word.",
    "- During each attempt, you are given the option to select a letter, guess the word as a whole, or quit.",
    " - If you guessed a word, you will only be informed whether the guess is correct or incorrect.",
    " - If you guessed a letter, the word will be partially filled with the chosen letter in the correct position(s), if the word has it.",
    " - If you quit, the game ends and the word is revealed."
]]
$interactionReply[
    {color:f89147}
    {author:$nickname:$authorAvatar}
    {title:Guess the Word}
    {thumbnail:$getImage[1163496192500568075;1]}
    {desc:Welcome to a game of *Guess the Word*. Please read the instructions below before starting the game.}
    {field:How to Play:$objectValues[#NL#]}
    {button:Start:blurple::guessword-start-$authorID}
    {button:Quit:red::guessword-quit0-$authorID}
]
$setUserVar[wordGuessSession;{
    "guess_count":0,
    "max_guess":$numGuess,
    "session":{
        "channel":"$channelID",
        "message":"$sentMessageID"
    }
}]
/*
    Type: On Modal Submit
    Modal ID: guessnumber
    Required Permission: None
    Ignored Roles: None
    Run only in: None
    Channel Used: Run in Channel
*/
$function[getImage;id;i]{
    $let[index;$conditional[$i!=undefined;$i;0]]
    $return[$msg[1157421810288185384;$id;attachment;$index]]
}
$let[errorIcon;$getImage[1157974096198508604;6]]
$let[gameIcon;$getImage[1163496192500568075;0]]
$let[guess;$modalAnswer[guess]]
$objectCreate[$getUserVar[numGuessSession]]
$if[$isNumber[$guess]==false]{
    $let[reason;`$guess` is not a number]
$else
    $let[guessNumber;$math[$guess]]
    $let[maxNumber;$objectGet[max_number]]
    $if[$regexCheck[$guessNumber;^\d+\.\d+$]==true]
        $let[reason;You must choose an integer]
    $elseIf[$guessNumber<0||$guessNumber>$maxNumber]
        $let[reason;You must choose a number between **0** and **$maxNumber**]
    $endElseIf
    $endIf
}
$onlyIf[$get[reason]==undefined;
    {private}
    {color:db3539}
    {author:Error:$errorIcon}
    {desc:$reason. Please try again.}
]
$let[targetNumber;$objectGet[target_number]]
$let[isCorrectAnswer;$checkCondition[$guessNumber==$targetNumber]]
$if[$isCorrectAnswer==true]{
    $removeButtons[$messageID]
    $editMessage[$messageID;
        {color:479f4b}
        {author:$nickname:$authorAvatar}
        {thumbnail:$gameIcon}
        {title:Guess the Number}
        {desc:Congratulations! You have guessed the number **$targetNumber**.}
        {footer:You Win#NL#Difficulty\: $objectGet[difficulty]#NL#Tries\: $objectGet[guess_count]:$getImage[1157974096198508604;7]}
    ]
    $deleteUserVar[numGuessSession]
$else
    $let[state;$conditional[$guessNumber>$targetNumber;high;low]]
    $let[maxNumber;$objectGet[max_number]]
    $arrayCreate[$seq[$objectGet[guess_count];1;-1]; ;index]
    $let[guessCount;$objectGet[guess_count]]
    $let[remGuess;$math[$objectGet[max_guess]-$guessCount]]
    $let[isGameOver;$checkCondition[$remGuess==0]]
    $objectIncrease[guess_count;1]
    $forEach[parameter;number,state;,]{
        $if[$objectKeyExists[history;$parameter]==true]
            $arrayCreate[$objectGet[history;$parameter];,;$parameter]
        $endIf
        $let[newEntry;$conditional[$parameter==number;$guessNumber;$toLocaleUppercase[$state]]]
        $arrayUnshift[$newEntry;$parameter]
        $objectSet[history;$parameter;$arrayJoin[,;$parameter]]
    }
    $setUserVar[numGuessSession;$getObject]
    $if[$isGameOver==true]
        $deleteUserVar[numGuessSession]
        $removeButtons[$messageID]
        $editMessage[$messageID;
            {color:db3539}
            {author:$nickname:$authorAvatar}
            {thumbnail:$gameIcon}
            {title:Game Over}
            {desc:Sorry! You have failed to guess the number. The number is **$targetNumber**.}
            {footer:You Lose#NL#Difficulty\: $objectGet[difficulty]:$errorIcon}
        ]
    $else
        $editMessage[$messageID;
            {color:$conditional[$state==high;ff3131;06038d]}
            {author:$nickname:$authorAvatar}
            {thumbnail:$gameIcon}
            {title:Guess the Number}
            {desc:- Your guess **$guessNumber** is too __**$state**__.#NL#- Please guess another number between **0** and **$maxNumber**.}
            {field:#:$arrayJoin[#NL#;index]:yes}
            {field:Guess:$arrayJoin[#NL#;number]:yes}
            {field:Result:$arrayJoin[#NL#;state]:yes}
            {footer:Difficulty\: $objectGet[difficulty]#NL#Remaining Guesses\: $remGuess}
        ]
    $endIf
}
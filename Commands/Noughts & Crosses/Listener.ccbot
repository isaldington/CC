/*
    Type: On Button Click
    Trigger: ttt-
    Min Perms: None
    Ignore Roles: None
    Run only in: #game-room
    channelUsed: Run in Channel
*/
$objectCreate[[
    [
        0,
        3,
        6
    ],
    [
        0,
        4
    ],
    [
        0,
        5,
        7
    ],
    [
        1,
        3
    ],
    [
        1,
        4,
        6,
        7
    ],
    [
        1,
        5
    ],
    [
        2,
        3,
        7
    ],
    [
        2,
        4
    ],
    [
        2,
        5,
        6
    ]
]]
$arrayCreate[$buttonID;-;button]
$let[sessionID;$arrayGet[2;button]]
$let[turn;$math[$arrayGet[3;button]+1]]
$let[curPlayer;$arrayGet[4;button]]
$let[buttonIndex;$arrayGet[5;button]]
$onlyIf[$authorID==$curPlayer;
    {private}
    {color:db3539}
    {author:Error}
    {desc:It is not currently your turn.}
]
$let[playerList;$msg[$channelID;$messageID;embedfieldvalue]]
$deleteMessage[$messageID]
$arrayCreate[$getMessageVar[buttonFill;$sessionID];,;fill]
$forEach[i;$seq[1;2]]{
    $let[player$i;$regexMatch[$playerList;\d{18,};g;$math[$i-1]]]
    $if[$get[player$i]!=$authorID]{
        $let[curPlayer;$get[player$i]]
    $else
        $let[playerIndex;$i]
    }
}
$arraySet[$math[$buttonIndex+1];$playerIndex;fill]
$setMessageVar[buttonFill;$arrayJoin[,;fill];$sessionID]
$let[modifier;$conditional[$playerIndex==1;1;-1]]
$arrayCreate[$getMessageVar[score;$sessionID];,;score]
$forEach[idx;$objectValues[,;$buttonIndex];,]{
    $let[n;$math[$idx+1]]
    $arraySet[$n;$math[$arrayGet[$n;score]+$modifier];score]
}
$setMessageVar[score;$arrayJoin[,;score];$sessionID]
$includeLibrary[ttt]
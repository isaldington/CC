/*
    Type: Button
    Trigger: xnochallenge-
    Min Perms: None
    Ignore Roles: None
    Run only in: #game-room
    channelUsed: Run in Channel
*/
$arrayCreate[$buttonID;-;button]
$let[target;$arrayGet[2;button]]
$let[action;$arrayGet[3;button]]
$let[errorIcon;$msg[1157421810288185384;1157974096198508604;attachment;6]]
$onlyIf[$authorID==$target;
    {private}
    {color:db3539}
    {author:Error:$errorIcon}
    {desc:Only the target may respond to the challenge.}
]
$disableButtons[$messageID]
$if[$action==accept]{
    $let[challenger;$regexMatch[$msg[$channelID;$messageID;embeddesc];\d{18,}]]
    $let[sessionID;$messageID]
    $interactionReply[
        <@$challenger>
        {color:479f4b}
        {author:Challenge Accepted:$msg[1157421810288185384;1157974096198508604;attachment;7]}
        {desc:<@$target> has accepted your challenge.}
    ]
    $arrayCreate[$challenger,$target;,;player]
    $arrayLoop[player;i;player]{
        $let[index;$random[1;$arrayLength[player]]]
        $let[player$i;$arrayGet[$index;player]]
        $arrayRemove[$index;player]
        $resetRandom
    }
    $let[turn;1]
    $let[curPlayer;$player1]
    $setMessageVar[buttonFill;0,0,0,0,0,0,0,0,0;$sessionID]
    $setMessageVar[score;0,0,0,0,0,0,0,0;$sessionID]
    $includeLibrary[xno]
$elseif[$action==decline]
    $let[challenger;$regexMatch[$msg[$channelID;$messageID;embeddesc];\d{18,}]]
    $interactionReply[
        <@$challenger>
        {color:transparent}
        {thumbnail:$errorIcon}
        {title:Challenge Declined}
        {desc:Unfortunately, $mention has declined your challenge.}
    ]
$endelseif
}
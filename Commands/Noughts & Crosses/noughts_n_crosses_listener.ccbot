/*
    Type: Button
    Button ID: ttt-
    Required Permission: None
    Ignore Roles: None
    Run only in: None
    Channel Used: Run in Channel
*/
$arrayCreate[$buttonID;-;button]
$let[sessionID;$arrayGet[2;button]]
$let[turn;$math[$arrayGet[3;button]+1]]
$let[curPlayer;$arrayGet[4;button]]
$let[buttonIndex;$arrayGet[5;button]]
$onlyIf[$authorID==$curPlayer;
    {private}
    {color:db3539}
    {author:Error:$msg[1157421810288185384;1157974096198508604;attachment;6]}
    {desc:It is not currently your turn.}
]
$objectCreate[{
    "button":[
        [
            0,
            3,
            6
        ],
        [
            0,
            4
        ],
        [
            0,
            5,
            7
        ],
        [
            1,
            3
        ],
        [
            1,
            4,
            6,
            7
        ],
        [
            1,
            5
        ],
        [
            2,
            3,
            7
        ],
        [
            2,
            4
        ],
        [
            2,
            5,
            6
        ]
    ],
    "data":$getMessageVar[tttSession;$sessionID]
}]
$let[playerList;$msg[$channelID;$messageID;embedfieldvalue]]
$arrayPush[$messageID;delete]
$if[$objectKeyExists[data;challenge]==true]{
    $arrayPush[$objectGet[data;challenge];delete]
    $objectRemove[data;challenge]
}
$forEach[id;$arrayJoin[,;delete];,]{
    $deleteMessage[$id]
}
$forEach[i;$seq[1;2]]{
    $let[player$i;$regexMatch[$playerList;\d{18,};g;$math[$i-1]]]
    $if[$get[player$i]!=$authorID]{
        $let[curPlayer;$get[player$i]]
    $else
        $let[playerIndex;$i]
    }
}
$objectSet[data;fill;$buttonIndex;$playerIndex]
$let[modifier;$conditional[$playerIndex==1;1;-1]]
$objectLoop[;idx;;button;$buttonIndex]{
    $objectIncrease[data;score;$idx;$modifier]
}
$setMessageVar[tttSession;$objectGet[data];$sessionID]
$includeLibrary[ttt]
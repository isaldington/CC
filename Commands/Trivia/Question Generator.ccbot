/*
    Type: Library
    Trigger: trivia
    Min Perms: None
    Ignore Roles: None
    Run only in: #game-room
    channelUsed: Run in Channel
*/
$function[getImage;id;i]{
    $if[$i==undefined]{
        $let[i;0]
    }
    $return[$msg[1157421810288185384;$id;attachment;$i]]
}
$function[resetData]{
    $deleteMessageVar[data;$messageID]
    $deleteUserVar[triviaSession]
}
$let[apiResponse;$jsonRequest[https://opentdb.com/api.php?amount=1&encode=url3986&token=$sessionToken&difficulty=$difficulty]]
$let[decodedResponse;$uri[decode;$apiResponse]]
$let[output;$regexReplace[$decodedResponse;(?<="question":".+)"(?=.+,"correct_answer");g;\"]]
$let[errorIcon;$getImage[1157974096198508604;6]]
$let[triviaIcon;$getImage[1162081466755334154;0]]
$if[$isValidObject[$output]]{
    $objectCreate[$output]
}
$let[responseCode;$objectGet[response_code]]
$if[$responseCode==0]{
    $let[correctAnswer;$objectGet[results;0;correct_answer]]
    $if[$objectGet[results;0;type]==multiple]
        $arrayCreate[$objectValues[,;results;0;incorrect_answers];,;answers]
        $arrayPush[$correctAnswer;answers]
        $arrayShuffle[answers]
    $else
        $arrayCreate[True,False;,;answers]
    $endIf
    $let[correctIndex;$arraySearch[$correctAnswer;answers]]
    $let[buttons;$arrayLoop[answer;i;answers]{
        $let[isCorrectAnswer;$checkCondition[$i==$correctIndex]]
        $objectSet[data;buttons;$i;$answer]
        {button:$answer:grey::trivia-answer-$i}
    }]
    $objectSet[data;correct_answer;$correctAnswer]
    $objectSet[data;player;$authorID]
    $objectSet[data;choice_count;$arrayLength[answers]]
    $interactionEdit[
        {color:$diffColor}
        {author:Question #$sessionNumber ($toLocaleUppercase[$objectGet[results;0;difficulty]])}
        {thumbnail:$triviaIcon}
        {title:$objectGet[results;0;question]}
        {desc:**Timeout** - <t\:$math[$timestamp[yes]+$timeLimit]\:R>}
        {footer:Score\: $score}
        $buttons
    ;$messageID]
    $let[data;$objectGet[data]]
    $setMessageVar[data;$data;$messageID]
    $wait[$get[timeLimit]s]
    $let[sessionData;$getUserVar[triviaSession]]
    $onlyIf[$sessionData!=undefined]
    $objectCreate[$sessionData]
    $onlyIf[$sessionNumber==$objectGet[session_no]&&$objectGet[timer_active]==true]
    $objectCreate[$data]
    $resetData
    $let[newMessage;
        {color:db3539}
        {author:Timeout:$errorIcon}
        {title:$msg[$channelID;$messageID;embedtitle]}
        {desc:**Correct answer\:** `$correctAnswer`#NL##NL#You have failed to answer in time. Your final score is `$score`.}
        $objectLoop[index;value;;buttons]{
            $let[style;$conditional[$value==$correctAnswer;green;grey]]
            {button:$value:$style::$index:no:yes}
        }
    ]
$elseif[$responseCode>=3]
    $resetData
    $objectCreate[[
        {
            "color":"transparent",
            "author":{
                "icon":6
            },
            "title":"Session Expired",
            "desc":"Unfortunately, your session has expired. Your final score is `$score`. Please start a new game."
        },
        {
            "color":"479f4b",
            "author":{
                "icon":7
            },
            "title":"Game Completed",
            "desc":"Congratulations! You have answered all the questions. Your final score is `$score`."
        }
    ]]
    $let[objectIndex;$math[$responseCode-3]]
    $let[newMessage;
        $objectLoop[parameter;value;;$objectIndex]{
            $if[$parameter==author]
                {author::$getImage[1157974096198508604;$objectGet[$objectIndex;author;icon]]}
            $else
                {$parameter:$value}
            $endIf
        }
    ]
$endelseif
$elseif[$responseCode==undefined]
    $let[newMessage;
        {color:transparent}
        {title:Error}
        {desc:An error has occured while loading the question. Please click on the button below to retrieve a new question.}
        {button:Refresh:green::trivia-next-$authorID}
    ]
$endelseif
$else
    $resetData
    $let[newMessage;
        {color:db3539}
        {author:Error:$errorIcon}
        {desc:Unfortunately, an unknown error has occured. Your final score is `$score`. Please start a new game.}
    ]
}
$interactionEdit[
    {thumbnail:$triviaIcon}
    $newMessage
;$messageID]
/*
    Type: On Menu Select
    Menu ID: trivia-
    Required Permission: None
    Ignored Roles: None
    Run only in: None
    Channel Used: Run in Channel
*/
$onlyIf[$regexCheck[$menuID;-$authorID$]==true;
    {private}
    {color:db3539}
    {author:Error:$msg[1157421810288185384;1157974096198508604;attachment;6]}
    {desc:This is not your session. Please call </trivia\:1161955757869248532> to start your own session.}
]
$deleteUserVar[triviaSession]
$if[$eventSelected==quit]
    $deleteMessage[$messageID]
$else
    $objectCreate[{
        "easy":{
            "color":"78b159",
            "time":15
        },
        "medium":{
            "color":"fdcc59",
            "time":30
        },
        "hard":{
            "color":"dd2d44",
            "time":60
        }
    }]
    $let[sessionToken;$jsonRequest[https://opentdb.com/api_token.php?command=request;token]]
    $let[difficulty;$eventSelected]
    $let[score;0]
    $let[sessionNumber;1]
    $let[diffColor;$objectGet[$difficulty;color]]
    $let[timeLimit;$objectGet[$difficulty;time]]
    $setUserVar[triviaSession;{
        "color":"$diffColor",
        "difficulty":"$difficulty",
        "score":"$score",
        "session":{
            "channel":"$channelID",
            "message":"$messageID"
        },
        "session_no":"$sessionNumber",
        "time_limit":"$timeLimit",
        "timer_active":true,
        "token":"$sessionToken"
    }]
    $removeMenu[$messageID]
    $includeLibrary[trivia]
$endIf
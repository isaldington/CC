/*
    Type: On Button Click
    Trigger: trivia-answer-
    Min Perms: None
    Ignore Roles: None
    Run only in: #game-room
    channelUsed: Run in Channel
*/
$function[getImage;id;i]{
    $if[$i==undefined]{
        $let[i;0]
    }
    $return[$msg[1157421810288185384;$id;attachment;$i]]
}
$objectCreate[{
    "data":$getMessageVar[data;$messageID],
    "outcome":{
        "true":{
            "color":"479f4b",
            "author":{
                "title":"Correct",
                "icon":"$getImage[1157974096198508604;7]"
            },
            "desc":"**Your answer\:** `$buttonLabel`\n\nYou have answered correctly. Please click on `Next Question` to get the next question."
        },
        "false":{
            "color":"db3539",
            "author":{
                "title":"Incorrect",
                "icon":"$getImage[1157974096198508604;6]"
            }
        }
    }
}]
$onlyIf[$objectGet[data;player]==$authorID;
    {private}
    {color:db3539}
    {author:Error:$getImage[1157974096198508604;6]}
    {desc:Only the player may answer this question. Please call </trivia\:1161955757869248532> to start your own session.}
]
$let[correctAnswer;$objectGet[data;correct_answer]]
$let[score;$objectGet[data;score]]
$let[choiceCount;$objectGet[data;choice_count]]
$let[difficulty;$objectGet[data;difficulty]]
$let[sessionToken;$objectGet[data;token]]
$objectSet[outcome;false;desc;**Your answer\:** `$buttonLabel`#NL#**Correct answer\:** `$correctAnswer`#NL##NL#You have answered incorrectly. Your final score is `$score`.]
$let[isCorrectAnswer;$checkCondition[$buttonLabel==$correctAnswer]]
$interactionEdit[
    {thumbnail:$getImage[1162081466755334154;0]}
    {title:$msg[$channelID;$messageID;embedtitle]}
    $objectLoop[parameter;value;;outcome;$isCorrectAnswer]{
        $if[$parameter==author]{
            {author:$objectValues[:;outcome;$isCorrectAnswer;author]}
        $else
            {$parameter:$value}
        }
    }
    $objectLoop[index;value;i;data;buttons]{
        $if[$value==$correctAnswer]{
            $let[style;green]
        $elseif[$value==$buttonLabel]
            $let[style;red]
        $endelseif
        $else
            $let[style;grey]
        }
        {button:$value:$style::$index:no:yes}
        $if[$i==$choiceCount&&$isCorrectAnswer==true]{
            {button:Next Question:grey::trivia-next-$authorID:yes}
        }
    }
    $if[$isCorrectAnswer==true]{
        $let[newScore;$math[$score+1]]
        $setUserVar[triviaSession;$difficulty|$newScore|$sessionToken]
        {footer:Score\: $newScore}
    $else
        $deleteMessageVar[currentScore;$messageID]
        $deleteUserVar[triviaSession]
    }
;$messageID]
$deleteMessageVar[data;$messageID]
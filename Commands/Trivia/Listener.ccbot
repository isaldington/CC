/*
    Type: On Button Click
    Button ID: trivia-answer-
    Required Permission: None
    Ignored Roles: None
    Run only in: None
    Channel Used: Run in Channel
*/
$function[getImage;id;i]{
    $if[$i==undefined]{
        $let[i;0]
    }
    $return[$msg[1157421810288185384;$id;attachment;$i]]
}
$let[object;{
    "data":$getMessageVar[data;$messageID],
    "user":$getUserVar[triviaSession;$authorID],
    "outcome":{
        "true":{
            "color":"479f4b",
            "icon":7,
            "desc":"**Your answer\:** `$buttonLabel`\n\nYou have answered correctly. Please click on `Next Question` to get the next question or `Quit` to quit the game."
        },
        "false":{
            "color":"db3539",
            "icon":6
        }
    }
}]
$objectCreate[$object]
$onlyIf[$objectGet[data;player]==$authorID;
    {private}
    {color:db3539}
    {author:Error:$getImage[1157974096198508604;6]}
    {desc:Only the player may answer this question. Please call </trivia\:1161955757869248532> to start your own session.}
]
$let[correctAnswer;$objectGet[data;correct_answer]]
$let[choiceCount;$objectGet[data;choice_count]]
$let[score;$objectGet[user;score]]
$objectSet[outcome;false;desc;**Your answer\:** `$buttonLabel`#NL#**Correct answer\:** `$correctAnswer`#NL##NL#You have answered incorrectly. Your final score is `$score`.]
$let[isCorrectAnswer;$checkCondition[$buttonLabel==$correctAnswer]]
$if[$isCorrectAnswer==true]{
    $let[newScore;$math[$score+1]]
    $objectSet[user;score;$newScore]
    $objectSet[user;timer_active;false]
    $setUserVar[triviaSession;$objectGet[user]]
$else
    $let[newScore;$score]
    $deleteUserVar[triviaSession]
}
$deleteMessageVar[data;$messageID]
$interactionEdit[
    {thumbnail:$getImage[1162081466755334154;0]}
    {title:$msg[$channelID;$messageID;embedtitle]}
    $objectLoop[parameter;value;;outcome;$isCorrectAnswer]{
        $if[$parameter==icon]{
            {author:$msg[$channelID;$messageID;embedauthortext]:$getImage[1157974096198508604;$value]}
        $else
            {$parameter:$value}
        }
    }
    $objectLoop[index;value;i;data;buttons]{
        $if[$value==$correctAnswer]{
            $let[style;green]
        $elseif[$value==$buttonLabel]
            $let[style;red]
        $endelseif
        $else
            $let[style;grey]
        }
        {button:$value:$style::$index:no:yes}
        $if[$i==$choiceCount&&$isCorrectAnswer==true]{
            {button:Next Question:blurple:ðŸ’¡:trivia-next-$authorID:yes}
            {button:Quit:grey:ðŸšª:trivia-quit-$authorID}
        }
    }
    {footer:Score\: $newScore}
;$messageID]
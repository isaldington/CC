/*
    Type: On Join/Leave
    When: User join the server
    Channel Used: #sandbox
*/
$let[createdDate;$user[$authorID;created]]
$let[createdMS;$math[$formatDate[$createdDate;x]-$parseTime[2h]]]
$let[createdS;$math[round($createdMS/1000)]]
$let[age;$math[$timestamp-$createdMS]]
$if[$age<$parseTime[1w]]{
    $arrayCreate[$getChannelVar[webhook;859961383261306890];, ;webhook]
    $objectCreate[{
        "User":"<@$authorID>",
        "Created":"<t#COLON#$createdS#COLON#d> <t#COLON#$createdS#COLON#t>",
        "Age":"$user[$authorID;timestamp]",
        "Minimum Age":"`7` days"
    }]
    $objectLoop[header;body]{
        $arrayPush[**$header**\: $body;desc]
    }
    $sendWebhook[
        {id=$arrayGet[1;webhook]}
        {token=$arrayGet[2;webhook]}
        {msg=
            {color:db3539}
            {author:$username[$authorID]:$userAvatar[$authorID]}
            {thumbnail:$msg[1157421810288185384;1157972358620004352;attachment;8]}
            {title:New Account Kicked}
            {desc:$arrayJoin[#NL#;desc]}
            {footer:User ID\: $authorID}
        }
    ]
    $kick[$authorID;New account]
$else
    $let[xDim;1920]
    $let[yDim;1080]
    $let[textXPos;1280]
    $let[textYPos;330]
    $let[textColor;000000]
    $imageCreate[$xDim;$yDim]
    $objectCreate[[
	    {
	        "type":"image",
	        "name":"background",
	        "url":"$msg[1157421810288185384;1198926241282138112;attachment]",
	        "parameter":{
	            "x_pos":0,
	            "y_pos":0,
	            "x_dim":$xDim,
	            "y_dim":$yDim
	        }
	    },
	    {
	        "type":"text",
	        "alignment":"center",
	        "font":"Rubik",
	        "size":80,
	        "weight":"bold",
	        "baseline":"bottom",
	        "content":"$displayName,",
	        "parameter":{
	            "x_pos":$textXPos,
	            "y_pos":$textYPos,
	            "color":"$textColor"
	        }
	    },
	    {
	        "type":"text",
	        "alignment":"center",
	        "font":"Rubik",
	        "size":80,
	        "weight":"regular",
	        "baseline":"bottom",
	        "content":"Welcome to DIKs & HOTs!\nYou are the $ordinal[$membersCount] member.",
	        "parameter":{
	            "x_pos":$textXPos,
	            "y_pos":$math[$textYPos+100],
	            "color":"$textColor"
	        }
	    }
    ]]
    $includeLibrary[image_draw]
    $channelSendMessage[1198927780591706152;$mention
	    {attachment:welcome.png:$imageOutput}
    ]
}
/*
    Type: Library
    Library Name: message_content
*/
$function[imageAction;action;type;text]{
	$if[$type==image]{
		$let[regex;https?://.+\.(gif|jpe?g|png|webp)(\?.+)?$]
	$elseIf[$type==tenor&&$action==get]
		$let[regex;(?<=https://tenor\.com/view/.+-gif-)\d{7,}]
	$endElseIf
	$elseIf[$type==tenor]
		$let[regex;<?https://tenor\.com/view/.+-gif-\d{7,}>?]
	$endElseIf
	}
	$if[$action==check]{
		$return[$regexCheck[$text;$regex;i]]
	$elseIf[$action==filter]
		$return[$regexReplace[$text;$regex;gi;]]
	$endElseIf
	$elseIf[$action==get&&$type==image]
		$return[$regexMatch[$text;$regex;i]]
	$endElseIf
	$elseIf[$action==get&&$type==tenor]
		$let[id;$regexMatch[$text;$regex;i]]
		$objectCreate[$httpRequest[https://tenor.googleapis.com/v2/posts?key=AIzaSyCVsUd5qj_vcMGbBdeLSFVirHkBEILsSLw&client_key==ccbot_messagecontent&ids=$id&media_filter=gif;GET]]
		$return[$objectGet[results;0;media_formats;gif;url]]
	$endElseIf
	}
}
$arrayCreate[AllAttachments,AllAttachmentsName,Author,AuthorName,ChannelName,Content,Created,Embed,Reference,URL;,;property]
$arrayLoop[property;;property]{
	$let[msg$property;$msg[$msgChannel;$msgID;$toLowercase[$property]]]
}
$let[msgEmbed;$regexReplace[$msgEmbed;^{thumbnail:.+}\n{url:.+}$;;]]
$arrayCreate[$msgAllAttachments;#NL#;attachment]
$arrayCreate[$msgAllAttachmentsName;#NL#;attachment_name]
$arrayLoop[file;i;attachment]{
	$if[$imageAction[check;image;$file]==true]{
		$arrayPush[$file;image]
	}
}
$if[$arrayLength[image]>0]{
	$let[msgImage;$arrayShift[image]]
}
$forEach[type;image,tenor;,]{
	$if[$imageAction[check;$type;$msgContent]==true]{
		$let[content;$imageAction[get;$type;$msgContent]]
		$if[$arrayLength[image]>0]
			$arrayUnshift[$content;image]
		$else
			$let[msgImage;$content]
		$endIf
		$let[msgContent;$imageAction[filter;$type;$msgContent]]
	}
}
$if[$get[msgImage]==undefined]{
	$let[msgImage;]
	$if[$msgContent==&&$regexCheck[$msgAllAttachmentsName;^(https?://.+\.(gif|jpe?g|png|webp)(\?.+)?)?$;m]==false]
		$let[msgContent;`This message only contain attachments.`]
	$elseIf[$msgContent==&&$msgEmbed==&&$arrayLength[image]==0]
		$let[msgContent;`Message cannot be displayed.`#NL#[Link to original message]($msgURL)]
	$endElseIf
	$endIf
}
$arraySlice[1;10;image]
$if[$msgEmbed!=]{
	$let[msgEmbed;$regexReplace[$msgEmbed;(?<=footer:[^:]+):;; -]]
	$let[embed2;$msgEmbed]
	$arrayRemove[$arrayLength[image];image]
	$if[$msgContent==&&$msgImage==]
		$let[msgContent;`This message only has embeds. Please see below.`]
	$endIf
$else
	$let[embed2;]
}
$if[$messageExists[$msgChannel;$msgReference]==true]{
	$forEach[option;Attachment,Author,AuthorName,Content,URL;,]{
		$let[msgRef$option;$msg[$msgChannel;$msgReference;$toLowercase[$option]]]
	}
	$let[msgRefHyperlink;[Link to original message]($msgRefURL)]
	$if[$imageAction[check;image;$msgRefContent]==false&&$msgRefAttachment==undefined&&$msgRefContent==]
		$let[msgRefContent;`Message cannot be displayed`.#NL#$msgRefHyperlink]
		$let[msgRefFile;]
	$else
		$if[$imageAction[check;image;$msgRefContent]==true]
			$let[msgRefFile;[Image]($imageAction[get;image;$msgRefContent])]
			$let[msgRefContent;$imageAction[filter;image;$msgRefContent]]
		$elseIf[$imageAction[check;tenor;$msgRefContent]==true]
			$let[msgRefFile;[Image]($imageAction[get;tenor;$msgRefContent])]
			$let[msgRefContent;$imageAction[filter;tenor;$msgRefContent]]
		$endElseIf
		$elseIf[$msgRefAttachment!=undefined]
			$let[msgRefFile;[Attachment]($msgRefAttachment)]
		$endElseIf
		$else
			$let[msgRefFile;]
		$endIf
		$let[msgRefContent;$regexReplace[$msgRefContent;(?<=.{100}|\n).*?$;s;…#NL#$msgRefHyperlink]]
	$endIf
	$let[msgRefContent;$regexReplace[$msgRefContent;(?<=\n);g;> ]]
	$let[msgRefAuthor;$conditional[$userExists[$msgRefAuthor]==true;<@$msgRefAuthor>;`$msgRefAuthorName`]]
	$let[msgRef;> ### **Replied to** $msgRefAuthor#NL#> $msgRefContent $msgRefFile#NL##NL#]
$else
	$let[msgRef;]
}
$if[$userExists[$msgAuthor]==true||$getMessageVar[authorID;$msgID]!=undefined]{
	$if[$getMessageVar[authorID;$msgID]!=undefined]
		$let[msgAuthor;$getMessageVar[authorID;$msgID]]
	$endIf
	$if[$nickname[$msgAuthor]!=$username[$msgAuthor]]
		$let[authName;$nickname[$msgAuthor]]
	$else
		$ignoreErrors[yes;$username[$msgAuthor]]
		$let[authName;$displayName[$msgAuthor]]
	$endIf
	$let[authAvatar;$userAvatar[$msgAuthor;;;yes]]
	$let[authColor;$userRoleColor[$msgAuthor]]
	$let[starboardee;$msgAuthor]
$else
	$let[authName;$msgAuthorName]
	$let[authIcon;$msg[$msgChannel;$msgID;authoravatar]]
	$let[authAvatar;$conditional[$authIcon!=undefined;$authIcon;]]
	$let[authColor;transparent]
	$let[starboardee;`$msgAuthorName`]
}
$if[$getTrigger[type]==word]{
	$arrayFilter[file;;attachment]{
		$checkCondition[$imageAction[check;image;$file]==false]
	}
	$let[msgAttachments;$arrayLoop[file;i;attachment]{
		{attachment:$arrayGet[$i;attachment_name]:$file}
	}]
$else
	$let[msgAttachments;]
}
$let[channelRegex;$conditional[$regexCheck[$msgChannelName;『.+』];$regexMatch[$msgChannelName;(?<=『).+(?=』)];$msgChannelName]]
$let[embedTemplate;
	{color:$authColor}
	{author:$authName:$authAvatar:$msgURL}
	{image:$msgImage}
	{footer:#$channelRegex}
	{timestamp:$dateToTime[$msgCreated]}
]
$let[msgImages;$arrayLoop[file;i;image]{
	{embed:
		$embedTemplate
		{image:$file}
	}
}]
$let[output;
	$msgAttachments
	{embed:
		$embedTemplate
		{desc:$msgRef$msgContent}
	}
	$msgImages
	{embed:$embed2}
]
/*
    Type: On Reaction
    Trigger: add, ‚≠ê
    Min Perms: Kick
    Ignore Roles: None
    Run only in: None
    channelUsed: Run in Channel
*/
$let[starboardChannel;1100392045392183378]
$onlyIf[$channelID!=$starboardChannel&&$regexCheck[$getServerVar[starboard];(?<!\d)$messageID(?!\d)]==false]
$if[$checkContains[$channel[$channelID;type];thread]==true]{
    $let[threadID;$channelID]
    $let[chnlID;$channel[$channelID;parentid]]
$else
    $let[threadID;]
    $let[chnlID;$channelID]
}
$onlyIf[$getChannelVar[webhook;$chnlID]!=undefined]
$let[webhookName;Starboard]
$let[webhookIcon;$msg[1157421810288185384;1157972910200332339;attachment;6]]
$let[msgChannel;$channelID]
$let[msgID;$messageID]
$forEach[id;$starboardChannel,$chnlID;,]{
    $arrayCreate[$getChannelVar[webhook;$id];, ]
    $arrayPush[$arrayGet[1];webhook]
    $arrayPush[$arrayGet[2];webhook]
    $arrayClear
}
$includeLibrary[message_content]
$let[starboardID;$sendWebhook[
    {id=$arrayGet[1;webhook]}
    {token=$arrayGet[2;webhook]}
    {username=$webhookName}
    {avatar=$webhookIcon}
    {msg=$output}
    {return_id=yes}
]]
$sendWebhook[
    {id=$arrayGet[3;webhook]}
    {token=$arrayGet[4;webhook]}
    {thread=$threadID}
    {username=$webhookName}
    {avatar=$webhookIcon}
    {msg=<@$authorID> has starboarded [a message]($msgURL) by <@$starboardee>. See all [starboarded messages](<https://discord.com/channels/$guild[id]/$starboardChannel>).}
]
$objectCreate[{
    "embed":{
        "author":"$starboardee",
        "color":"ffb134",
        "desc":{
            "Starboardee":"<@$starboardee>",
            "Starboarder":"$mention"
        },
        "footer":"$messageID",
        "title":"Message Starboard",
        "url":"$msgURL"
    }
}]
$includeLibrary[modlog]
$arrayCreate[$getServerVar[starboard];,;starboard]
$if[$arrayLength==200]{
    $arrayRemove[1;starboard]
}
$arrayPush[$messageID;starboard]
$setServerVar[starboard;$arrayJoin[,;starboard]]
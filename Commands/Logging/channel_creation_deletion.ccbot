/*
    Type: Channel Creation/Deletion
    When: Channel is created/deleted
    Required Permission: None
    Ignore Roles: None
    Run only in: None
    Channel Used: #sandbox
*/
$objectCreate[{
	"channel_type":{
		"forum":"Forum",
		"news":"Announcement",
		"stage":"Stage",
		"text":"Text",
		"voice":"Voice"
	}
}]
$let[action;$conditional[$channelExists[$eventChannelID]==true;Create;Delete]]
$let[embedColor;$conditional[$action==Create;32cd32;d0342c]]
$let[chnlType;$channel[$eventChannelID;type]]
$if[$chnlType==category]{
	$let[channelClass;Category]
$elseif[$regexCheck[$chnlType;^thread|post]==true]
	$let[channelClass;Thread]
$endelseif
$else
	$let[channelClass;Channel]
}
$let[eventChannel;$conditional[$action==Delete||$chnlType==category;$channel[$eventChannelID;name];<#$eventChannelID>]]
$objectSet[embed;desc;$channelClass;$eventChannel]
$if[$channelClass==Channel]{
    $objectMerge[embed;desc;{
        "Category":"$channel[$eventChannelID;parentname]",
        "Type":"$objectGet[channel_type;$chnlType]"
    }]
	$if[$regexCheck[$chnlType;undefined|voice|stage]==false]
		$let[channel_topic;$channel[$eventChannelID;topic]]
		$objectSet[embed;desc;Topic;$conditional[$channel_topic!=none;$channel_topic;None]]
	$endIf
$elseif[$channelClass==Thread||$channelClass==Post]
    $objectMerge[embed;desc;{
        "Type":"$toLocaleUppercase[$regexMatch[$chnlType;(?<=thread_)\w+$|post]]",
        "Channel":"<#$eventChannelParent>",
        "Category":"$channel[$eventChannelParent;parentname]",
        "Created By":"<@$thread[$eventChannelID;owner]>",
        "Duration":"$thread[$eventChannelID;duration]"
    }]
$endelseif
}
$if[$action==Delete]{
	$let[channelCreated;$math[round($channel[$eventChannelID;timestamp]/1000)]]
	$let[shortTime;$conditional[$timestamp-$channelCreated<86400;<t\:$channelCreated\:t>;]]
	$let[channelAge;$parseDate[$math[$timestamp-1000*$channelCreated];time]]
	$objectMerge[embed;desc;{
	    "Created":"<t\:$channelCreated\:d> $shortTime",
	    "Age":"$regexMatch[$channelAge;^(?:\d+\x20[a-z]+(?:\x20(?:and\x20)?|$)){1,2}]"
    }]
}
$objectMerge[embed;{
    "color":"$embedColor",
    "footer":"$eventChannelID",
    "title":"$channelClass $action"
}]
$includeLibrary[modlog]